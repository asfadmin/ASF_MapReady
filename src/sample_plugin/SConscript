import os.path

Import("globalenv")
localenv = globalenv.Clone()

localenv.AppendUnique(CPPPATH = [
        "#src/asf/",
        "#include/",
        "#src/asf_meta/",
        "#src/libasf_proj/",
        ])

localenv.ParseConfig("pkg-config --cflags --libs libgeotiff")
localenv.ParseConfig("pkg-config --cflags --libs gsl")

localenv.AppendUnique(LIBS = [
    "asf",
    "asf_proj",
    "asf_meta",
])

head_build_dir, tail_build_dir = os.path.split(Dir(".").path)

localenv.AppendUnique(LIBPATH = [
    "#" + os.path.join(head_build_dir, "asf"),
    "#" + os.path.join(head_build_dir, "libasf_proj"),
    "#" + os.path.join(head_build_dir, "asf_meta"),
])


bins = localenv.Program("sample_plugin", ["sample_plugin.c"])

globalenv.Install(globalenv["inst_dirs"]["bins"], bins)

# run the unit test, and set its dependencies
test_result = localenv.UnitTest(
    [
        "sample_plugin.t.c",
        "test_main.t.c",
    ],
    [bins],
    LIBS = ["cunit"],
    LIBPATH = [],
    RPATH = []
)
