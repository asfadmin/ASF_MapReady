import os.path

Import("globalenv")
localenv = globalenv.Clone()

localenv.AppendUnique(CPPPATH = [
    "#src/asf/",
    "#include/",
    "#src/asf_meta/",
    "#src/libasf_proj/",
    "#src/libasf_geocode/",
    "#src/libasf_raster/",
])

localenv.ParseConfig("pkg-config --cflags --libs libglade-2.0")
localenv.ParseConfig("pkg-config --cflags --libs libgeotiff")
localenv.ParseConfig("pkg-config --cflags --libs gsl")

localenv.AppendUnique(LIBS = [
    "asf",
    "asf_proj",
    "asf_meta",
])

head_build_dir, tail_build_dir = os.path.split(Dir(".").path)

localenv.AppendUnique(LIBPATH = [
    "#" + os.path.join(head_build_dir, "asf"),
    "#" + os.path.join(head_build_dir, "libasf_proj"),
    "#" + os.path.join(head_build_dir, "asf_meta"),
])


bins = localenv.Program("proj2proj", [
    "proj2proj.c",
    "utility.c",
    "font.c",
    "projfile.c",
    "geocode.c",
    "execute.c",
    "new.c",
])

globalenv.Install(globalenv["inst_dirs"]["bins"], bins)

shares = localenv.File([
    "proj2proj.glade",
    "proj2proj_icon.ico",
])

globalenv.Install(globalenv["inst_dirs"]["shares"], shares)
